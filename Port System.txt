@skip isdbref(tag(setr(1,port)))={@assert/inline isdbref(globalroom())=@pemit %#=ERROR: No master room set!;@assert/inline isdbref(setr(0,create(Port System,,t)))=@pemit %#=ERROR: Could not create code object %q1: %q0;@set %q0=INDESTRUCTIBLE !NO_COMMAND;@tag/add %q1=%q0;@tel %q0=globalroom()}

&CMD.PORT #port=$^(?s)(?\:\+)(port|ic|return|getlost)(?\:/(\\S+)?)?(?\: +(.+?))?(?\:=(.*))?$:th setq(!,switch(%1,ic,return,%1),cmd);@attach #inc/GET_SWITCH=%2,%q<cmd>;@attach %!/SW.%q<cmd>.[strfirstof(%q<switch>,MAIN)]=%3,%4
@set #port/CMD.PORT=regexp

&SWITCHES.PORT.0 #port=LIST|CONFIG|OOC

&SW.PORT.CONFIG #port=@attach #inc/CONFIG=%0,trim(%1),%!

&CONFIG.OOC.DESC #port=Dbref of the Location that'll be used for +port/ooc
&CONFIG.OOC.DEFAULT #port=#-1
&CONFIG.OOC.TYPE #port=DBREF

&DO_PORT #port=@remit [loc(%0)]=[cname(%0)] vanishes in a flash using %2.;@tel %#=%1;@remit %1=[cname(%0)] appears in a flash using %2.

&DO_PORT_CHECK #port=@assert/inline cand(isdbref(%1),hastype(%1,ROOM),isdbref(upzone(%1)))=@pemit %0=That's not a good target.;@assert/inline elock(%1/TportLock,%0)=@pemit %0=Permission denied. You don't pass the TportLock.;@break/inline strmatch(objid(%1),objid(loc(%0)))=@pemit %0=You're already there!;@attach %!/DO_PORT=%0,%1,%2
@@ %0 mover, %1 target, command name

&SW.PORT.MAIN #port=@if !strlen(%0)={@attach %!/SW.LIST},{@if isdbref(setr(!,pmatch(%0),player))={th setq(!,objeval(%#,loc(%q<player>),2),target)},{th setq(!,%0,target)};@attach %!/DO_PORT_CHECK=%#,%q<target>,+port}

&SW.GETLOST.MAIN #port=@assert/inline isdbref(setr(!,u(FN.RANDROOM,%#),randroom))=@pemit %#=No accessible rooms found to get lost in.;@attach %!/DO_PORT=%#,%q<randroom>,+getlost;

&SW.PORT.OOC #port=@assert/inline isdbref(setr(!,num(getconf(%!,OOC)),oocroom))=@pemit %#=No OOC room configured.;@attach %!/DO_PORT_CHECK=%#,%q<oocroom>,+port/ooc;


&FN.LIST_ROOMS #port=iter(%0,filter(FIL.ROOM,lzone(%i0)),%B,%B)

&FIL.ACCESSIBLE #port=elock(%0/TportLock,%1)

&FN.RANDROOM #port=randextract(squish(iter(shuffle(u(#reg/FN.SEARCH)),iter(shuffle(u(#reg/FN.ROOMS,%i0)),if(u(FIL.ACCESSIBLE,%i0,%0),objid(%i0)[ibreak(1)][ibreak(0)])))),1)

&FIL.SEARCH #port=cand(u(FIL.ACCESSIBLE,%0,%1),strmatch(name(%0),*%2*))

&LIST_REGION #port=@if words(setr(!,sortname(filter(FIL.[if(strlen(%1),SEARCH,ACCESSIBLE)],u(#reg/FN.ROOMS,%0),%b,%b,%#,%1)),rooms))={@pemit %#=separator([iter(revwords(upzones(%0)) %0,cname(%i0),%b,%b-%b)]);@pemit %#=columns(iter(%q<rooms>,ljust(%i0,7) [cname(%i0)],%b,|),37,2,l,,1,,,,1,|,,1)};@if words(setr(!,u(#reg/FN.BRANCHES,%0),sub))={@dolist/inline %q<sub>=@attach %!/LIST_REGION=%d0,%1}

&SW.PORT.LIST #port=@if strlen(%0)=@pemit %#=header(if(strlen(%0),Rooms Matching: *%0*,Room List));@if words(setr(!,u(#reg/FN.ROOTS),roots))={@dolist/inline %q<roots>={@attach %!/LIST_REGION=%d0,%0}};@pemit %#=footer(To filter: +port/list <name>)

&SW.RETURN.MAIN #port=th setq(!,get(%#/GAME.SWEPT_FROM),dest);@assert/inline isdbref(%q<dest>)=@pemit %#=You have no recorded return location.;@attach %!/DO_PORT_CHECK=%#,%q<dest>;@set %#=GAME.SWEPT_FROM:

&HLP.PORT #port=A handy tool for quickly teleporting around the game grid to publically accessible locations.%R%R[ansi(hc,Commands)]%R%R[ansi(hw,+port/list \[<name>\])]%R%TDisplays (and optionally filters by name) candidate rooms.%R%R[ansi(hw,+port <target>)]%R%TTeleport to a specific room. <target> must be the room's dbref.%R%TAlternatively, <target> can be a player, if you have the permissions to locate player and they're in a room that you could access via +port.%R%RFeeling adventurous? Try [ansi(hw,+getlost)] to be teleported to a random location you have access to.%R%R[ansi(hw,+port/ooc)]%RTakes you to the OOC nexus.
+help/add Navigation/+port=#port/HLP.PORT

+shelp/add Navigation/+port=#port/SHLP.PORT
&SHLP.PORT #port=+port controls access to destinations by the TportLock on Rooms. The default TportLock on Rooms in this codesuite is to check the TportLock on the Upzones in sequence and, if any allow it, the lock passes. This means it's most effective to control access to root @regions while sub-regions offer finer control.%R%R[ansi(hw,+port/config <option>=<value>)]%RConfigure the +port system.

+help/add Navigation/+ic=#port/HLP.IC
&HLP.IC #port=[ansi(hw,+ic)]%RUse this command to return to your last location before you being swept idle. also responds to [ansi(hw,+return)].

+help/add Navigation/+getlost=#port/HLP.GETLOST
&HLP.GETLOST #port=[ansi(hw,+getlost)]%RUse this command to teleport to a random location on the grid that you have access to via +port.