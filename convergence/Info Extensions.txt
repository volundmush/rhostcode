@dolist/inline/delimit | abi~Ability|res~Resource|npc~NPC|com~Complication|ext~Extra|app~Application|copy~Copy={@skip isdbref(tag(setr(1,[before(%d0,~)]_info)))={@assert/inline isdbref(globalroom())=@pemit %#=ERROR: No global room set!;@assert/inline isdbref(setr(0,create(after(%d0,~) Info System,,t)))=@pemit %#=ERROR: Could not create code object: %q0;@set %q0=INDESTRUCTIBLE !NO_COMMAND;@tag/add %q1=%q0;@tel %q0=[globalroom()];@parent %q0=#info_lib}}

&CMD.ABILITY #abi_info=$^(?s)(?\:\+)(abi|ability)(?\:/(\\S+)?)?(?\: +(.+?))?(?\:/(.+?)?)?(?\:=(.*))?$:@attach %!/DO_INFO
@set #abi_info/CMD.ABILITY=regexp

&INFO.ATTR #abi_info=ABI
&INFO.NAME #abi_info=Ability
&INFO.PLURAL #abi_info=Abilities
&INFO.CMD #abi_info=+ABILITY
&INFO.CMD_SHORT #abi_info=+abi
&INFO.LOCKAPPROVE #abi_info=1

&SWITCHES.INFO.4 #abi_info=setunion(u(parent(%!)/SWITCHES.INFO.4),VALUE,|,|)

&SW.INFO.VALUE #abi_info=@assert/inline strlen(%q<filename>)=@pemit %#=[v(INFO.NAME)] name empty.;@attach #inc/VALID.INT=%2;@dolist/inline/nobreak/delimit | %q<filename>={@attach %!/INC.INFO.FINDFILE=%d0;@set %q<target>=%q<attr>.VALUE:%q<value>;@attach %!/REPORT=%#,%q<target>,%q<fname>,Value set to: %q<value>}

&CMD.RESOURCE #res_info=$^(?s)(?\:\+)(res|resource)(?\:/(\\S+)?)?(?\: +(.+?))?(?\:/(.+?)?)?(?\:=(.*))?$:@attach %!/DO_INFO
@set #res_info/CMD.RESOURCE=regexp

&INFO.ATTR #res_info=RES
&INFO.NAME #res_info=Resource
&INFO.PLURAL #res_info=Resources
&INFO.CMD #res_info=+RESOURCE
&INFO.CMD_SHORT #res_info=+res
&INFO.LOCKAPPROVE #res_info=1

&CMD.NPC #npc_info=$^(?s)(?\:\+)(npc)(?\:/(\\S+)?)?(?\: +(.+?))?(?\:/(.+?)?)?(?\:=(.*))?$:@attach %!/DO_INFO
@set #npc_info/CMD.NPC=regexp

&INFO.ATTR #npc_info=NPC
&INFO.NAME #npc_info=NPC
&INFO.PLURAL #npc_info=NPCs
&INFO.CMD #npc_info=+NPC
&INFO.CMD_SHORT #npc_info=+npc
&INFO.LOCKAPPROVE #npc_info=1

&CMD.COMPLICATION #com_info=$^(?s)(?\:\+)(com|complication)(?\:/(\\S+)?)?(?\: +(.+?))?(?\:/(.+?)?)?(?\:=(.*))?$:@attach %!/DO_INFO
@set #com_info/CMD.COMPLICATION=regexp

&INFO.ATTR #com_info=COM
&INFO.NAME #com_info=Complication
&INFO.PLURAL #com_info=Complications
&INFO.CMD #com_info=+COMPLICATION
&INFO.CMD_SHORT #com_info=+com
&INFO.LOCKAPPROVE #com_info=1

&CMD.EXTRA #ext_info=$^(?s)(?\:\+)(ext|extra)(?\:/(\\S+)?)?(?\: +(.+?))?(?\:/(.+?)?)?(?\:=(.*))?$:@attach %!/DO_INFO
@set #ext_info/CMD.EXTRA=regexp

&INFO.ATTR #ext_info=EXT
&INFO.NAME #ext_info=Extra
&INFO.PLURAL #ext_info=Extra Data
&INFO.CMD #ext_info=+EXTRA
&INFO.LOCKAPPROVE #ext_info=1

&CMD.APPLICATION #app_info=$^(?s)(?\:\+)(app|application)(?\:/(\\S+)?)?(?\: +(.+?))?(?\:/(.+?)?)?(?\:=(.*))?$:@attach %!/DO_INFO
@set #app_info/CMD.APPLICATION=regexp

&INFO.ATTR #app_info=APP
&INFO.NAME #app_info=Application
&INFO.PLURAL #app_info=Application Info
&INFO.CMD #app_info=+APPLICATION
&INFO.LOCKAPPROVE #app_info=1

&CMD.COPY #copy_info=$^(?s)(?\:\+)(cop|copy)(?\:/(\\S+)?)?(?\: +(.+?))?(?\:/(.+?)?)?(?\:=(.*))?$:@attach %!/DO_INFO
@set #copy_info/CMD.COPY=regexp

&INFO.ATTR #copy_info=COPY
&INFO.NAME #copy_info=Copy
&INFO.PLURAL #copy_info=Copied Powers
&INFO.CMD #copy_info=+COPY
&INFO.CMD_SHORT #copy_info=+cop

@skip isdbref(tag(setr(1,superpower)))={@assert/inline isdbref(globalroom())=@pemit %#=ERROR: No glogbal room set!;@assert/inline isdbref(setr(0,create(Super Power Sheet Tracker,,t)))=@pemit %#=ERROR: Could not create code object %q1: %q0;@set %q0=INDESTRUCTIBLE !NO_COMMAND;@tag/add %q1=%q0;@tel %q0=[globalroom()]}

&CMD.LIST #superpower=$^(?s)(?\:\+)(list|sheet)(?\:/(\\S+)?)?(?\: +(.+?))?(?\:/(.+?)?)?(?\:=(.*))?$:th setq(!,%1,family);@attach #inc/GET_SWITCH=%2,%q<family>;@attach %!/SW.%q<family>.[strfirstof(%q<switch>,MAIN)]=trim(%3),trim(%4),%5
@set #superpower/CMD.LIST=regexp

&SWITCHES.LIST.0 #superpower=FULL

&ABILITIES #superpower=tag(abi_info)
&COPY #superpower=tag(copy_info)
&RESOURCES #superpower=tag(res_info)
&NPCS #superpower=tag(npc_info)
&COMPLICATIONS #superpower=tag(com_info)
&EXTRA #superpower=tag(ext_info)

&DO_LIST #superpower=@attach #inc/GET_PLAYER=strfirstof(%0,%#),target;th setq(!,,choice);@if strlen(%1)={@attach #inc/PARTIAL=%1,ABILITIES|COPY|RESOURCES|NPCS|EXTRA|COMPLICATIONS,|,choice,choice};@dolist/inline/delimit | [strfirstof(%q<choice>,ABILITIES|COPY|RESOURCES|NPCS|EXTRA|COMPLICATIONS)]={th setq(!,u(%d0),obj);@if words(setr(!,u(%q<obj>/FN.LIST_ATTRS_ORDER,%q<target>),attrs))={@pemit %#=header(cname(%q<target>)'s [get(%q<obj>/INFO.PLURAL)][if(%2,%B- Full)]);@dolist/inline %q<attrs>={th setq(!,get(%q<target>/%d0.BODY),body);th setq(!,get(%q<target>/%d0.SUMMARY),summary);@pemit %#=separator(get(%q<target>/%d0.NAME) - [if(%2,Body,if(strlen(%q<summary>),Summary,Body))]);@pemit %#=if(%2,%q<body>,strfirstof(%q<summary>,%q<body>))}}};@pemit %#=footer()

&SW.LIST.MAIN #superpower=@attach %!/DO_LIST=%0,%1,0
&SW.LIST.FULL #superpower=@attach %!/DO_LIST=%0,%1,1

&DO_SHEET #superpower=@attach #inc/GET_PLAYER=strfirstof(%0,%#),target;@attach #inc/PARTIAL=%1,ABILITIES|COPY|RESOURCES|NPCS|EXTRA|COMPLICATIONS,|,choice,choice;@dolist/inline/delimit | [strfirstof(%q<choice>,ABILITIES|COPY|RESOURCES|NPCS|EXTRA|COMPLICATIONS)]={th setq(!,u(%d0),obj);@if words(setr(!,u(%q<obj>/FN.LIST_ATTRS_ORDER,%q<target>),files),|)={@pemit %#=header(cname(%q<target>)'s [u(%q<obj>/INFO.PLURAL)]);@pemit %#=columns(iter(%q<files>,left(\([default(%q<target>/%i0.ORDER,?)]\)%B[if(setr(!,get(%q<target>/%i0.VALUE),val),[chr(40)][if(gt(%q<val>,0),ansi(hg,+%q<val>),ansi(hr,-%q<val>))][chr(41)]%B)][get(%q<target>/%i0.NAME)],39),%b,|),39,2,l,1,1,,%b,,1,|,%B,1)[if(strlen(%q<files>),%R%R[center(setr(!,lcstr(u(%q<obj>/INFO.CMD_SHORT)),cmd) [cname(%q<target>)]/<entry>,setr(!,floor(fdiv(width(%#),2)),half))][center(+list/full [cname(%q<target>)]/[after(%q<cmd>,+)],%q<half>)])]}};@pemit %#=footer(+sheet [cname(%q<target>)])

&SW.SHEET.MAIN #superpower=@attach %!/DO_SHEET=%0,%1,0
&SW.SHEET.ALL #superpower=@attach %!/DO_SHEET=%0,%1,1

&HLP.SHEET #superpower=Convergence uses an extension of the +info file system for handling character sheets/abilities/details for roleplay. These are:%R%R[ansi(hw,+abi)] aka +ability%R[ansi(hw,+res)] aka +resource%R[ansi(hw,+npc)] aka +npcs (non-player characters)%R[ansi(hw,+com)] aka +complications%R[ansi(hw,+ext)] for anything +extra.%R[ansi(hw,+app)] for +application details.%R[ansi(hw,+cop)] for +copy powers, which remains unlocked after approval.%R%RThese all work just like the +info command does, and setting /summary entries for them is very good practice if they get large.%R%R[ansi(hc,Additional Commands)]%R[ansi(hw,+sheet <player>)]%R%TDisplay the index of a player character's sheet files.%R%R[ansi(hw,+list <player>)]%R%TDisplay the details of each sheet file. Defaults to showing the /summary if available, full body if not. This can be VERY SPAMMY to other players and staff if Summaries are not set, so that's why you should set /summary entries!%R%R[ansi(hw,+list/full <player>)]%R%Tlike normal +list, but displays both summary and body.%R%RBoth [ansi(hw,+sheet)] and [ansi(hw,+list)] support specifying the category to display by targeting as <player>/<category>, where <category> could be Abi, Res, etc.
+help/add Character/Sheet=#superpower/HLP.SHEET