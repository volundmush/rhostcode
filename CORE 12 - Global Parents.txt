@@ Core
@cmdquota me=99999

@skip isdbref(tag(setr(1,gm)))={@assert/inline isdbref(globalroom())=@pemit %#=ERROR: No master room set!;@assert/inline isdbref(setr(0,create(Wizard Doing It,u(#acc/FUN_RANDOM_PASSWORD),p)))=@pemit %#=ERROR: Could not create code object %q1: %q0;@set %q0=INDESTRUCTIBLE SIDEFX INHERIT GUILDMASTER NO_MODIFY FREE;@power/guild %q0=free_quota;@tag/add %q1=%q0;@tel %q0=[tag(stor)];@limit/dmod %q0=-1}

@dolist/inline/delimit | globthing~Thing|globexit~Exit|globplayer~Player|globroom~Room=@skip isdbref(tag(setr(1,before(%d0,~))))={@assert/inline isdbref(tag(stor))=@pemit %#=ERROR: No storage object installed!;@assert/inline isdbref(setr(0,create(Global Parent [after(%d0,~)],,t)))=@pemit %#=ERROR: Could not create code object: %q0;@set %q0=SAFE SIDEFX INHERIT VISUAL INDESTRUCTIBLE NO_MODIFY NO_COMMAND;@tag/add %q1=%q0;@tel %q1=[tag(stor)];@chown/preserve %q0=[tag(gm)]}

&LOCK_ZONEWIZLOCK #globthing=[switch(bittype(%#),>1,t(match(v(BUILDERS),%:)),>4,1,0)]
&LOCK_ZONETOLOCK #globthing=[u(LOCK_ZONEWIZLOCK,%0,%1)]

@@ General Objects
&GAME_ALEAVE #globobj=@if cand(isdbref(setr(!,if(hasflag(%!,ZONEMASTER),%!,upzone(%!)),oldzone)),!strmatch(%q<oldzone>,setr(!,upzone(%#),newzone)))={th gm_upzone_remove(%q<oldzone>,%:)}
&GAME_AENTER #globobj=@if cand(isdbref(setr(!,if(hasflag(%!,ZONEMASTER),%!,upzone(%!)),upzone)),!strmatch(%q<upzone>,setr(!,get(%#/GAME_UPZONE),oldz)))={th gm_upzone_add(%q<upzone>,%:)}

@@ Players
@power/guild #globplayer=FORMATTING

@@ Rooms
@power/guild #globroom=FORMATTING

@aenter #globroom=@attach #globobj/GAME_AENTER
@aleave #globroom=@attach #globobj/GAME_ALEAVE

&FUN_LOCATION #globroom=iter(revwords(upzones(%!)),cname(%i0)[if(gtebittype(%#,2),%B\([num(%i0)]\))],%b,%b=>%b)

&FUN_INFOBOX #globroom=[wrap([ansi(hw,Location:)]%b[u(FUN_LOCATION,%!)],sub(width(%#),2),left,setr(!,ansi(confoverride(%#,BORDER),|),bord),%q<bord>,,)]

@nameformat #globroom=[header(cname(%!)[if(gtebittype(%#,2),%B\(%!\))],%#,,1)][if(strlen(setr(!,u(FUN_INFOBOX),infobox)),%R%q<infobox>)]
&formatdesc #globroom=[separator(Description,%#,,1)]%R[wrap(%0,sub(width(%#),2),left,setr(!,ansi(confoverride(%#,BORDER),|),bord),%q<bord>,,)]

&FIL_TOTEM #globroom=hastotem(%0,%1)

&FUN_GETNAME #globroom=cname(%0)[if(controls(%#,%0),%B([num(%0)]))]

&FUN_FORMATOBJ_COLUMN #globroom=if(gtebittype(%#,2),printf($1.&:%3:s $-5s $-|"25s $-|"42s $1.&:%3:s,%R,ansi(%2,Dbref),ansi(%2,Name),ansi(%2,Short-Desc),%R),printf($1.&:%3:s $-|"25s $-|"44s $1.&:%3:s,%R,ansi(%2,Name),ansi(%2,Short-Desc),%R))

&FUN_FORMATOBJ_ROW #globroom=if(gtebittype(%#,2),printf($1.&:%3:s $-5s $-|"25s $-|"42s $1.&:%3:s,%R,num(%0),cname(%0),default(%0/short-desc,???),%R),printf($1.&:%3:s $-|"25s $-|"44s $1.&:%3:s,%R,cname(%0),default(%0/short-desc,???),%R))

&FUN_FORMATOBJ_COLUMN_PCS #globroom=if(gtebittype(%#,2),printf($1.&:%3:s $-5s $-|"21s $-|"37s $-4s $-3s $1.&:%3:s,%R,ansi(%2,Dbref),ansi(%2,Name),ansi(%2,Short-Desc),ansi(%2,Idle),ansi(%2,RP),%R),printf($1.&:%3:s $-|"21s $-|"38s $-4s $-3s $1.&:%3:s,%R,ansi(%2,Name),ansi(%2,Short-Desc),ansi(%2,Idle),ansi(%2,RP),%R))

&FUN_FORMATOBJ_ROW_PCS #globroom=if(gtebittype(%#,2),printf($1.&:%3:s $-5s $-|"21s $-|"37s $4s $-3s $1.&:%3:s,%R,num(%0),cname(%0),default(%0/short-desc,???),hideidle(%0),default(%0/RPSTATUS,OOC),%R),printf($1.&:%3:s $-|"33s $-|"29s $4s $-3s $1.&:%3:s,%R,cname(%0),default(%0/short-desc,???),hideidle(%0),default(%0/RPSTATUS,OOC),%R))

&FUN_FORMATSECTION #globroom=[separator(%0,%#,,1)]%R[if(hasattrp(%!/FUN_FORMATOBJ_COLUMN_%0),u(FUN_FORMATOBJ_COLUMN_%0,%0,%1,p%2,%3),u(FUN_FORMATOBJ_COLUMN,%0,%1,%2,%3))]%R[trim(iter(%1,if(hasattrp(%!/FUN_FORMATOBJ_ROW_%0),u(FUN_FORMATOBJ_ROW_%0,%i0,%1,%2,%3),u(FUN_FORMATOBJ_ROW,%i0,%1,%2,%3))),b,%r)]

&FIL_HASAFTER #globroom=t(words(after(%0,~)))

&TOTEM_FILTERS #globroom=PCs~PC|NPCs~NPC|Items~ITEM|Structures~STRUCTURE|Regions~Region

&FUN_FILTERCON #globroom=[trim(iter(v(TOTEM_FILTERS),setq(!,filter(%!/FIL_TOTEM,%0,%b,%b,after(%i0,~)),before(%i0,~)),|,%B))][setq(!,setdiff(%0,iter(v(TOTEM_FILTERS),r(before(%i0,~)),|,%B)),Objects)]

@conformat #globroom=[if(words(%0),[null(u(FUN_FILTERCON,%0))][setq(!,ansi(confoverride(%#,BORDER),|),bord)][setq(!,confoverride(%#,COLUMN),col)][trim(iter(filter(%!/FIL_HASAFTER,iter([v(TOTEM_FILTERS)]|Objects,[before(%i0,~)]~[r(before(%i0,~))],|,|),|,|),%R[u(FUN_FORMATSECTION,before(%i0,~),after(%i0,~),%q<col>,%q<bord>)],|,),b,%R)])]

&FUN_FORMATEXITS #globroom=separator(%0,%#,,1)%R[printf($1.&:%3:s $-5s $-12s $-55s $1.&:%3:s,%R,ansi(%1,Alias),ansi(%1,Name),ansi(%1,Destination),%R)]%R[trim(iter(%1,printf($1.&:%3:s $-5s $-12s $-55s $1.&:%3:s,%R,getalias(%i0),cname(%i0),cname(loc(%i0)),%R),%B,%R),b,%R)]

@exitformat #globroom=[if(cor(words(%0),words(%2)),[setq(!,confoverride(%#,COLUMN),col)][setq(!,ansi(confoverride(%#,BORDER),|),bord)][if(words(%0),u(FUN_FORMATEXITS,Exits,%0,%q<col>,%q<bord>)%R)][if(words(%2),[u(FUN_FORMATEXITS,Dark Exits,%2,%q<col>,%q<bord>)]%R)])][footer(,%#,,1)]


@@ Things
@power/guild #globthing=FORMATTING
@enter #globthing=You enter the [cname(%!)].
@oenter #globthing=enters the [cname(%!)].

@aenter #globthing=@attach #globobj/GAME_AENTER
@aleave #globthing=@attach #globobj/GAME_ALEAVE

@leave #globthing=You leave the [cname(%!)].
@oleave #globthing=leaves the [cname(%!)].
@oxleave #globthing=emerges from the [cname(%!)].

@@ Exits
@power/guild #globexit=FORMATTING
@success #globexit=You take the [cname(%!)] exit to [cname(loc(%!))].
@osuccess #globexit=takes the [cname(%!)] exit to [cname(loc(%!))].

&LOCK_BASIC #globexit=cor(gtebittype(%#,2),lmin(1 [iter(lattrp_full(%!,LOCK_BASIC_*),if(u(%!/%i0,%0,%1,%i0),1,0[ibreak()]))]))
@lock/basic #globexit=LOCK_BASIC/1