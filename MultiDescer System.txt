@@ Dependencies: Info Lib

@skip isdbref(tag(setr(1,mdesc)))={@assert/inline isdbref(globalroom())=@pemit %#=ERROR: No global room set!;@assert/inline isdbref(setr(0,create(MultiDescer System,,t)))=@pemit %#=ERROR: Could not create code object: %q0;@set %q0=INDESTRUCTIBLE;@tag/add %q1=%q0;@tel %q0=globalroom();@parent %q0=#info_lib}

&CMD.MDESC #mdesc=$^(?s)(?\:\+)(mdesc)(?\:/(\\S+)?)?(?\: +(.+?))?(/(.+?)?)?(?\:=(.*))?$:@attach %!/DO_INFO
@set #mdesc/CMD.MDESC=regexp

&SWITCHES.INFO.0 #mdesc=setunion(u(parent(%!)/SWITCHES.INFO.0),SWITCH|NOISY,|,|)

&INFO.ATTR #mdesc=DESC
&INFO.NAME #mdesc=MultiDescription
&INFO.PLURAL #mdesc=MultiDescriptions
&INFO.CMD #mdesc=+MDESC
&INFO.CMD_SHORT #mdesc=+mdesc
&INFO.INIT #mdesc=th setq(!,1,private)

&SW.INFO.SWITCH #mdesc=@assert/inline strlen(%q<filename>)=@pemit %#=Must enter a desc filename!;@attach %!/INC.INFO.FINDFILE=%q<filename>;@cpattr %q<target>/%q<attr>.BODY=%#/DESC;@if hasattr(%q<target>/%q<attr>.SUMMARY)=@cpattr %q<target>/%q<attr>.SUMMARY=%#/short-desc;@cpattr %q<target>/%q<attr>.NAME=%#/DESC.NAME;@set %#/DESC.NAME=visual;@if t(%3)={@remit %l=%k switches to %p [ansi(hw,%q<fname>)] description.},{@pemit %#=You quietly switched to your [ansi(hw,%q<fname>)] desc.}

&SW.INFO.NOISY #mdesc=@attach %!/SW.INFO.SWITCH=%0,%1,%2,1

&HLP.MDESC #mdesc=The MultiDescer system, or Mdesc, is used for storing multiple character appearances and managing your [ansi(hw,@desc)] and [ansi(hw,&short-desc)] attributes for you. This helps characters with vast wardrobes, transformations, alternate-forms and so on keep everything organized and re-usable.%R%ROur Multidescer shares all of the same code as the [ansi(hw,+info)] system for creating and administrating +mdesc files. So it's a good idea to check out [ansi(hw,+help +info)]%R%R[ansi(hc,Basic Commands)]%R[ansi(hw,+mdesc)]%RLists your current descriptions.%R%R[ansi(hw,+mdesc <name>)]%RDisplays a specific description.%R%R[ansi(hw,+mdesc <name>=<contents>)]%RCreates or updates a description.%R%R[ansi(hw,+mdesc/summary <name>=<contents>)]%RSets a short-desc to be paired with that description.%R%R[ansi(hc,Additional Commands)]%R[ansi(hw,+mdesc/switch <file>)]%R%TSwitches to a specific file. The file's contents will be assigned to your @desc, and, if set, the /summary will be set to your &short-desc attribute. The name will be set to your &DESC.NAME attribute.%R%R[ansi(hw,+mdesc/noisy <file>)]%R%TLike /switch but will announce to the room.
+help/add Character/+mdesc=#mdesc/HLP.MDESC