@skip isdbref(tag(setr(1,bucket_parent)))={@assert/inline isdbref(tag(stor))=@pemit %#=ERROR: No storage object set!;@assert/inline isdbref(setr(0,create(Ticket Bucket Parent,,t)))=@pemit %#=ERROR: Could not create code object %q1: %q0;@set %q0=INDESTRUCTIBLE NO_COMMAND;@tag/add %q1=%q0;@tel %q0=#stor}

@lock/user #bucket_parent|AdminLock=LK.GTEBITTYPE/5
@lock/user #bucket_parent|PostLock=LK.TRUE/1

&CONFIG.PERM #bucket_parent=@assert/inline elock(%2/user|AdminLock,%#)=@pemit %#=Permission denied.

&CONFIG.DUE.DESC #bucket_parent=Default due date on a Ticket for this bucket.
&CONFIG.DUE.DEFAULT #bucket_parent=1209600
&CONFIG.DUE.TYPE #bucket_parent=DURATION

@skip isdbref(tag(setr(1,tick)))={@assert/inline isdbref(globalroom())=@pemit %#=ERROR: No global room set!;@assert/inline isdbref(setr(0,create(Ticket Tracker,,t)))=@pemit %#=ERROR: Could not create code object %q1: %q0;@set %q0=INDESTRUCTIBLE !NO_COMMAND;@tag/add %q1=%q0;@tel %q0=[globalroom()]}

&CMD.TICKET #tick=$^(?s)(?\:\+)(job|myjob|myjobs|myticket|mytickets|jobs|ticket|bucket)?(?\:/(\\S+)?)?(?\: +(.+?))?(?\:=(.*))?$:th setq(!,switch(%1,job,ticket,jobs,ticket,ticket,ticket,myticket,mytickets,mytickets,mytickets,myjob,mytickets,myjobs,mytickets,bucket,bucket),family);@attach #inc/GET_SWITCH=%2,%q<family>;@attach %!/SW.%q<family>.[strfirstof(%q<switch>,MAIN)]=trim(%3),%4
@set #tick/CMD.TICKET=regexp

@@ families: ticket, mytickets, bucket

@@ Bucket Family
&SWITCHES.BUCKET.6 #tick=CREATE|RENAME|LOCK|UNLOCK|DELETE|DESCRIBE|GRANT|REVOKE|OPTIONS

&FIL.BUCKET_CANADMIN #tick=cor(elock(%0/user|AdminLock,num(%1)),gtebittype(%1,5),match(get(%0/STAFF),objid(%1)))
&FIL.BUCKET_CANPOST #tick=cor(elock(%0/user|PostLock,num(%1)),gtebittype(%1,5))

&REG_BUCKET_NAME #tick=[A-Za-z0-9]{2,12}

&FN.BUCKETS #tick=sortname(lcon(#bucket_parent,OBJECT,,,1))

&FN.VISIBLE_BUCKETS #tick=filter(FIL.BUCKET_CANPOST,u(FN.BUCKETS),%b,%b,%0)

&LIST_TICKETS #tick=@pemit %#=header(%0);@pemit %#=u(FN.LIST_COLUMNS);@if words(%1)={@dolist/inline %1={@pemit %#=u(FN.LIST_ROW,%d0)}};@pemit %#=footer()

&INC.BUCKET_DISPLAY #tick=@attach %!/FINDBUCKET=%0,bucket,ADMIN;@attach %!/LIST_TICKETS=All Tickets for: [cname(%q<bucket>)],filterobjid(get(%q<bucket>/TICKETS))

&FIL.STATUS #tick=strmatch(get(%0/STATUS),%1)

&COUNT #tick=words(filter(FIL.STATUS,get(%0/TICKETS),%b,%b,%1))

&FIL.OVERDUE #tick=cand(u(FIL.STATUS,%0,Pending),gte(%1,get(%0/DUE)))

&FN.BUCKET_COLUMNS #tick=ansi(confoverride(%#,COLUMN),printf($-12s $-41s $-3s $-3s $-3s $-3s $-3s $-3s,Name,Description,Pen,Com,Dny,Cnc,Ovr,Due))
&FN.BUCKET_ROW #tick=printf($-12s $-41s $:0:3s $:0:3s $:0:3s $:0:3s $:0:3s $3-s,cname(%0),get(%0/DESC),u(COUNT,%0,Pending),u(COUNT,%0,Completed),u(COUNT,%0,Denied),u(COUNT,%0,Canceled),words(filter(FIL.OVERDUE,get(%0/TICKETS),%b,%b,secs())),smalltime(getconf(%0,DUE)))

&INC.BUCKET_LIST #tick=@pemit %#=header(Buckets);@pemit %#=u(FN.BUCKET_COLUMNS);@if words(setr(!,u(FN.VISIBLE_BUCKETS,%:),vis))={@dolist/inline %q<vis>={@pemit %#=u(FN.BUCKET_ROW,%d0)}};@pemit %#=footer()

&SW.BUCKET.MAIN #tick=@if strlen(%0)={@attach %!/INC.BUCKET_DISPLAY},{@attach %!/INC.BUCKET_LIST}

&SW.BUCKET.CREATE #tick=@assert/inline strlen(%0)=@pemit %#=No name entered!;@assert/inline regmatchi(%0,v(REG_BUCKET_NAME))=@pemit %#=Buckets must be single words of 2-12 alphanumeric characters.;@break/inline isdbref(locate(#bucket_parent,%0,i))=@pemit %#=That name is already in use!;@assert/inline isdbref(setr(!,create(%0,,t),res))=@pemit %#=Couldn't create Bucket: %q<res>;@parent %q<res>=#bucket_parent;@tel %q<res>=#bucket_parent;@if has_markup(%0)={@name/ansi %q<res>=%0};@pemit %#=Bucket '[cname(%q<res>)]' created.;@attach #inc/MSG_ALERT=Bucket '[cname(%q<res>)]' created.

&FINDBUCKET #tick=@assert/inline strlen(%0)=@pemit %#=Must enter a bucket name!;@assert/inline words(setr(!,filter(FIL.BUCKET_CAN%2,u(FN.BUCKETS),%b,%b,%#),buckets))=@pemit %#=You can't see any buckets to select! Can you %2 it?;@assert/inline isobjid(setr(!,namegrab(%q<buckets>,%0),%1))=@pemit %#=No bucket by that name!

&SW.BUCKET.RENAME #tick=@attach %!/FINDBUCKET=%0,bucket,ADMIN;

&SW.BUCKET.LOCK #tick=@attach %!/FINDBUCKET=trim(before(%0,/)),bucket,ADMIN;@attach #inc/PARTIAL=after(%0,/),PostLock|AdminLock,|,lock;@attach #inc/VALID.LOCKKEY=%1;@lock/user %q<bucket>|%q<lock>=%q<value>;@pemit %#=Changed %q<lock> on Bucket '[cname(%q<bucket>)]' to: %q<value>;@attach #inc/MSG_ALERT=Changed %q<lock> on Bucket '[cname(%q<bucket>)]' to: %q<value>

&SW.BUCKET.UNLOCK #tick=@attach %!/FINDBUCKET=trim(before(%0,/)),bucket,ADMIN;@attach #inc/PARTIAL=after(%0,/),PostLock|AdminLock,|,lock;@unlock/user %q<bucket>|%q<lock>;@pemit %#=Restored Default Lock on Bucket '[cname(%q<bucket>)]';@attach #inc/MSG_ALERT=Restored Default Lock on Bucket '[cname(%q<bucket>)]'

&SW.BUCKET.DESCRIBE #tick=@attach %!/FINDBUCKET=%0,bucket,ADMIN;@assert/inline strlen(%1)=@pemit %#=Describe it as what?;@describe %q<bucket>=%1;@pemit %#=New description set for Bucket '[cname(%q<bucket>)]';@attach #inc/MSG_ALERT=New description set for Bucket '[cname(%q<bucket>)]'

&SW.BUCKET.GRANT #tick=@attach %!/FINDBUCKET=%0,bucket,ADMIN;@attach #inc/GET_PLAYER=%1,t1;@break/inline match(setr(!,filterobjid(get(%q<bucket>/STAFF)),staff),%q<t1>)=@pemit %#=[cname(%q<t1>)] is already a member of that Bucket's staff!;@set %q<bucket>=STAFF:[setunion(%q<staff>,%q<t1>)];@pemit %#=Added [cname(%q<t1>)] as Bucket '[cname(%q<bucket>)]' Staff.;@attach #inc/MSG_ALERT=Added [cname(%q<t1>)] as Bucket '[cname(%q<bucket>)]' Staff.;@pemit %q<t1>=You've been added as Staff to Bucket '[cname(%q<bucket>)]'

&SW.BUCKET.REVOKE #tick=@attach %!/FINDBUCKET=%0,bucket,ADMIN;@attach #inc/GET_PLAYER=%1,t1;@assert/inline match(setr(!,filterobjid(get(%q<bucket>/STAFF)),staff),%q<t1>)=@pemit %#=[cname(%q<t1>)] is not a member of that Bucket's staff!;@set %q<bucket>=STAFF:[setdiff(%q<staff>,%q<t1>)];@pemit %#=Removed [cname(%q<t1>)] from Bucket '[cname(%q<bucket>)]' Staff.;@attach #inc/MSG_ALERT=Removed [cname(%q<t1>)] from Bucket '[cname(%q<bucket>)]' Staff.;@pemit %q<t1>=You're no longer Staff for Bucket '[cname(%q<bucket>)]'

&SW.BUCKET.OPTIONS #tick=@attach %!/FINDBUCKET=before(%0,/),bucket,ADMIN;@attach #inc/CONFIG=trim(after(%0,/)),%1,%q<bucket>

@@ Ticket Family
&SWITCHES.TICKET.0 #tick=CREATE|NEW|REPLY|STAFFCOMMENT|CLAIM|UNCLAIM|ASSIGN|UNASSIGN|ADDHELPER|REMHELPER|COMPLETE|DENY|CANCEL|REOPEN|DUE|SCAN|NEXT|PENDING
&SWITCHES.TICKET.5 #tick=ARCHIVE

&TICKET_ALERT #tick=@if words(setr(!,filter(%1,lwhoid()),targets))={@pemit/list %q<targets>=[ansi(c,TICKETS:)] [ansi(hw,New Activity By)] %k [ansi(hw,For)] Ticket [cname(%q<ticket>)]: '[get(%q<ticket>/TITLE)]' - %0}

&HEAR_CREATE #tick=u(CAN_REPLY,%0,%1)

&SW.TICKET.CREATE #tick=@attach %!/FINDBUCKET=trim(before(%0,/)),bucket,POST;@assert/inline strlen(setr(!,trim(after(%0,/)),title))=@pemit %#=Must enter a title for the new ticket!;@assert/inline strlen(%1)=@pemit %#=Must enter a description for the ticket!;@assert/inline isdbref(setr(!,create(setr(!,inc(v(LASTID)),latest),,t),ticket))=@pemit %#=Error creating Ticket: %q<ticket>;@set %!=LASTID:%q<latest>;@set %q<ticket>=INDESTRUCTIBLE SAFE;@set %q<ticket>=BUCKET:%q<bucket>;@set %q<bucket>=TICKETS:[listunion(filterobjid(get(%q<bucket>/TICKETS)),objid(%q<ticket>))];@describe %q<ticket>=%1;@set %q<ticket>=TITLE:%q<title>;@set %q<ticket>=OWNER:%:;@set %q<ticket>=STATUS:Pending;@set %q<ticket>=DUE:[add(secs(),getconf(%q<bucket>,DUE))];@set %q<ticket>=LAST_CREATE:[csecs(%q<ticket>)];@attach %!/TICKET_ALERT=New Ticket created!,HEAR_REPLY

&SW.TICKET.NEW #tick=@attach %!/SW.TICKET.CREATE

&FN.TICKETS #tick=lcon(%!,OBJECT,,,1)

&FN.TICKET #tick=locate(%!,%0,Ti)

&FN.LASTCHECKED #tick=[lmax(iter(lattr(%0/LAST_*),if(u(CAN_[after(%i0,_)],%0,objid(%1)),get(%0/%i0),0)))]

&FN.NEWACTIVITY #tick=gte(u(FN.LASTCHECKED,%0,%1),default(%0/CHECKED_[num(%1)],0))

&FINDTICKET #tick=@assert/inline strlen(%0)=@pemit %#=Must enter a Ticket ID!;@assert/inline isdbref(setr(!,u(FN.TICKET,%0),%1))=@pemit %#=Cannot find Ticket: [switch(r(%1),#-2*,Ambiguous match!,#-1*,Not found!)];th setq(!,objid(r(%1)),%1);th setq(!,get(r(%1)),strfirstof(%2,bucket));

&ADDEVENT #tick=@set %0=EV.[rjust(setr(!,inc(get(%0/LASTID)),lastid),4,0)]:%2|%1|[cname(%1)]|[secs()]|%3;@set %0=LASTID:%q<lastid>;@set %0=LAST_%2:[secs()]

@@ %0 - ticket objid, %1 - objid of cause, %2 - type of event (REPLY, COMMENT, EVENT, CLAIM, UNCLAIM, etc), %3 - text

&FN.ACTION #tick=switch(%0,REPLY,replied,COMMENT,ansi(hr,internal staff commented),CLAIM,claimed,UNCLAIM,unclaimed,ASSIGN,assigned,UNASSIGN,unassigned,ADDHELPER,added a helper,REMHELPER,removed a helper,COMPLETE,completed,DENY,denied,CANCEL,canceled,REOPEN,re-opened,DUE,updated due,%0)

&CAN_SEE #tick=cor(strmatch(get(%0/OWNER),%1),match(setunion(get(%0/HANDLERS),get(%0/HELPERS)),%1),u(FIL.BUCKET_CANADMIN,get(%0/BUCKET),%1))

&FN.NAMES #tick=iter(filterobjid(%0),cname(%i0),%b,chr(44)%B)

&DISPLAY_TICKET #tick=@attach %!/FINDTICKET=%0,ticket;@assert/inline u(CAN_SEE,%q<ticket>,%:)=@pemit %#=Permission denied.;@pemit %#=header(Ticket [cname(%q<ticket>)]);@pemit %#=printf($-36s $-36s,ljust(rjust(ansi(h,Category:),10),10) [cname(get(%q<ticket>/BUCKET))],ljust(rjust(ansi(h,Due:),10),10) [fancytime(get(%q<ticket>/DUE))]);@pemit %#=printf($-36s $-36s,ljust(rjust(ansi(h,Title:),10),10) [get(%q<ticket>/TITLE)],ljust(rjust(ansi(h,Status:),10),10) [get(%q<ticket>/STATUS)]);@pemit %#=printf($-36s $-36s,ljust(rjust(ansi(h,Handlers:),10),10) [u(FN.NAMES,get(%q<ticket>/HANDLERS))],ljust(rjust(ansi(h,Helpers:),10),10) [u(FN.NAMES,get(%q<ticket>/HELPERS))]);@pemit %#=separator();@pemit %#=cname(get(%q<ticket>/OWNER)) [ansi(hw,created on [fancytime(csecs(%q<ticket>))]:)];@pemit %#=get(%q<ticket>/DESC);@dolist/inline sort(lattr(%q<ticket>/EV.*))={@if u(HEAR_[elements(setr(!,get(%q<ticket>/%d0),ev),1,|)],%:)={@pemit %#=separator();@pemit %#=if(isobjid(elements(%q<ev>,2,|)),cname(elements(%q<ev>,2,|)),elements(%q<ev>,3,|)%B\(Deleted\)) [ansi(hw,u(FN.ACTION,elements(%q<ev>,1,|)) on [fancytime(elements(%q<ev>,4,|))])];@pemit %#=after(%q<ev>,[elements(get(%q<ticket>/%d0),4,|)]|)}};@pemit %#=footer();@set %q<ticket>=CHECKED_%#:[secs()]

&FN.VISIBLE_TICKETS #tick=sort(filter(CAN_SEE,u(FN.TICKETS),%b,%b,objid(%0)))

&FN.LIST_COLUMNS #tick=ansi(confoverride(%#,COLUMN),printf($4s $-8s $-15s $-20s $-15s $-11s,ID,Bucket,Owner,Title,Handlers,Due))
&FN.LIST_ROW #tick=printf($4s $-8s $-15s $-20s $-15s $-11s,cname(%0),cname(get(%0/BUCKET)),if(isobjid(get(%0/OWNER)),cname(get(%0/OWNER)),get(%0/OWNER_NAME)%B\(Deleted\)),get(%0/TITLE),strfirstof(iter(filterobjid(get(%0/HANDLERS)),cname(%i0),%B,chr(44)%B),---),if(strmatch(get(%0/STATUS),Pending),fancytime(get(%0/DUE),,,$02M/$02D/$y),get(%0/STATUS))[if(u(FN.NEWACTIVITY,%0,%:),ansi(hr,*))])

&SW.TICKET.MAIN #tick=@if strlen(%0)={@attach %!/DISPLAY_TICKET},{@attach %!/LIST_TICKETS=Visible Tickets,u(FN.VISIBLE_TICKETS,%:)}

&CAN_REPLY #tick=cor(strmatch(get(%0/OWNER),%1),u(FIL.BUCKET_CANADMIN,get(%0/BUCKET),%1),match(setunion(get(%0/HANDLERS),get(%0/HELPERS)),%1))
&HEAR_REPLY #tick=u(CAN_REPLY,%0,%1)
@@ %0 - the ticket, %1 - objid of one being checked.

&SW.TICKET.REPLY #tick=@attach %!/FINDTICKET=%0,ticket;@assert/inline u(CAN_REPLY,%q<ticket>,%:)=@pemit %#=Permission denied.;@assert/inline strlen(%1)=@pemit %#=Must enter a reply!;@attach %!/ADDEVENT=%q<ticket>,%:,REPLY,%1;@attach %!/TICKET_ALERT=Reply added!,HEAR_REPLY

&CAN_COMMENT #tick=cor(u(FIL.BUCKET_CANADMIN,get(%0/BUCKET),%1),match(setunion(get(%q<ticket>/HANDLERS)),%1))
&HEAR_COMMENT #tick=u(CAN_COMMENT,%0,%1)

&SW.TICKET.STAFFCOMENT #tick=@attach %!/FINDTICKET=%0,ticket;@assert/inline u(CAN_COMMENT,%q<ticket>,%:)=@pemit %#=Permission denied.;@assert/inline strlen(%1)=@pemit %#=Must enter a comment!;@attach %!/ADDEVENT=%q<ticket>,%:,COMMENT,%1;@attach %!/TICKET_ALERT=Staff comment added!,HEAR_COMMENT

&CAN_CLAIM #tick=u(FIL.BUCKET_CANADMIN,get(%0/BUCKET),%1)
&HEAR_CLAIM #tick=u(CAN_CLAIM,%0,%1)

&SW.TICKET.CLAIM #tick=@attach %!/FINDTICKET=%0,ticket;@assert/inline u(CAN_CLAIM,%q<ticket>,%:)=@pemit %#=Permission denied.;@break/inline t(match(get(%q<ticket>/HANDLERS),%:))=@pemit %#=You are already working on '[cname(%q<ticket>)]: [get(%q<ticket>/TITLE)]';@set %q<ticket>=HANDLERS:[setunion(filterobjid(get(%q<ticket>/HANDLERS)),%:)];@attach %!/ADDEVENT=%q<ticket>,%:,CLAIM,Handler Appointed: %k;@attach %!/TICKET_ALERT=Claimed the Ticket.,HEAR_CLAIM

&CAN_UNCLAIM #tick=u(FIL.BUCKET_CANADMIN,get(%0/BUCKET),%1)
&HEAR_UNCLAIM #tick=u(CAN_UNCLAIM,%0,%1)

&SW.TICKET.UNCLAIM #tick=@attach %!/FINDTICKET=%0,ticket;@assert/inline u(CAN_UNCLAIM,%q<ticket>,%:)=@pemit %#=Permission denied.;@assert/inline t(match(get(%q<ticket>/HANDLERS),%:))=@pemit %#=You are not working on '[cname(%q<ticket>)]: [get(%q<ticket>/TITLE)]';@set %q<ticket>=HANDLERS:[setdiff(filterobjid(get(%q<ticket>/HANDLERS)),%:)];@attach %!/ADDEVENT=%q<ticket>,%:,UNCLAIM,Handler Removed: %k;@attach %!/TICKET_ALERT=Reversed claim of the Ticket.,HEAR_UNCLAIM

&CAN_ASSIGN #tick=u(FIL.BUCKET_CANADMIN,get(%0/BUCKET),%1)
&HEAR_ASSIGN #tick=u(CAN_ASSIGN,%0,%1)

&SW.TICKET.ASSIGN #tick=@attach %!/FINDTICKET=%0,ticket;@assert/inline u(CAN_ASSIGN,%q<ticket>,%:)=@pemit %#=Permission denied.;@attach #inc/GET_PLAYER=%1,t1;@break/inline t(match(get(%q<ticket>/HANDLERS),%q<t1>))=@pemit %#=[cname(%q<t1>)] is already working on '[cname(%q<ticket>)]: [get(%q<ticket>/TITLE)]';@set %q<ticket>=HANDLERS:[setunion(filterobjid(get(%q<ticket>/HANDLERS)),%q<t1>)];@attach %!/ADDEVENT=%q<ticket>,%:,ASSIGN,Handler Assigned: [cname(%q<t1>)];@attach %!/TICKET_ALERT=Assigned the Ticket to [cname(%q<t1>)],HEAR_ASSIGN

&CAN_UNASSIGN #tick=u(FIL.BUCKET_CANADMIN,get(%0/BUCKET),%1)
&HEAR_UNASSIGN #tick=u(CAN_UNASSIGN,%0,%1)

&SW.TICKET.UNASSIGN #tick=@attach %!/FINDTICKET=%0,ticket;@assert/inline u(CAN_UNASSIGN,%q<ticket>,%:)=@pemit %#=Permission denied.;@attach #inc/GET_PLAYER=%1,t1;@assert/inline t(match(get(%q<ticket>/HANDLERS),%q<t1>))=@pemit %#=[cname(%q<t1>)] is not working on '[cname(%q<ticket>)]: [get(%q<ticket>/TITLE)]';@set %q<ticket>=HANDLERS:[setdiff(filterobjid(get(%q<ticket>/HANDLERS)),%q<t1>)];@attach %!/ADDEVENT=%q<ticket>,%:,UNASSIGN,Handler Removed: [cname(%q<t1>)];@attach %!/TICKET_ALERT=Removed handlership from [cname(%q<t1>)],HEAR_UNASSIGN

&CAN_ADDHELPER #tick=cor(match(get(%0/CLAIMED),%1),u(FIL.BUCKET_CANADMIN,get(%0/BUCKET),%1))
&HEAR_ADDHELPER #tick=u(CAN_REPLY,%0,%1)

&SW.TICKET.ADDHELPER #tick=@attach %!/FINDTICKET=%0,ticket;@assert/inline u(CAN_ADDHELPER,%q<ticket>,%:)=@pemit %#=Permission denied.;@attach #inc/GET_PLAYER=%1,t1;@break/inline t(match(get(%q<ticket>/HELPERS),%q<t1>))=@pemit %#=[cname(%q<t1>)] is already working on '[cname(%q<ticket>)]: [get(%q<ticket>/TITLE)]';@set %q<ticket>=HELPERS:[setunion(filterobjid(get(%q<ticket>/HELPERS)),%q<t1>)];@attach %!/ADDEVENT=%q<ticket>,%:,ADDHELPER,Helper Assigned: [cname(%q<t1>)];@attach %!/TICKET_ALERT=Added [cname(%q<t1>)] as a helper.,HEAR_ADDHELPER

&CAN_REMHELPER #tick=cor(match(get(%0/CLAIMED),%1),u(FIL.BUCKET_CANADMIN,get(%0/BUCKET),%1))
&HEAR_REMHELPER #tick=u(CAN_REPLY,%0,%1)

&SW.TICKET.REMHELPER #tick=@attach %!/FINDTICKET=%0,ticket;@assert/inline u(CAN_REMHELPER,%q<ticket>,%:)=@pemit %#=Permission denied.;@attach #inc/GET_PLAYER=%1,t1;@assert/inline t(match(get(%q<ticket>/HELPERS),%q<t1>))=@pemit %#=[cname(%q<t1>)] is not working on '[cname(%q<ticket>)]: [get(%q<ticket>/TITLE)]';@set %q<ticket>=HELPERS:[setdiff(filterobjid(get(%q<ticket>/HELPERS)),%q<t1>)];@attach %!/ADDEVENT=%q<ticket>,%:,REMHELPER,Helper Removed: [cname(%q<t1>)];@attach %!/TICKET_ALERT=Removed [cname(%q<t1>)] as a helper.,HEAR_REMHELPER

&FINISH #tick=@attach %!/FINDTICKET=%0,ticket;@assert/inline u(CAN_%2,%q<ticket>,%:)=@pemit %#=Permission denied.;@assert/inline strlen(%1)=@pemit %#=Must enter a finishing message!;@attach %!/ADDEVENT=%q<ticket>,%:,%2,%1;@attach %!/TICKET_ALERT=%3 Ticket.,HEAR_%2

&CAN_COMPLETE #tick=cor(match(get(%0/CLAIMED),%1),u(FIL.BUCKET_CANADMIN,get(%0/BUCKET),%1))
&HEAR_COMPLETE #tick=u(CAN_REPLY,%0,%1)

&SW.TICKET.COMPLETE #tick=@attach %!/FINISH=%0,%1,COMPLETE,Completed;@set %q<ticket>=STATUS:Completed

&CAN_DENY #tick=cor(match(get(%0/CLAIMED),%1),u(FIL.BUCKET_CANADMIN,get(%0/BUCKET),%1))
&HEAR_DENY #tick=u(CAN_REPLY,%0,%1)

&SW.TICKET.DENY #tick=@attach %!/FINISH=%0,%1,DENY,Denied;@set %q<ticket>=STATUS:Denied

&CAN_CANCEL #tick=cor(strmatch(get(%0/OWNER),%1),match(get(%0/CLAIMED),%1),u(FIL.BUCKET_CANADMIN,get(%0/BUCKET),%1))
&HEAR_CANCEL #tick=u(CAN_REPLY,%0,%1)

&SW.TICKET.CANCEL #tick=@attach %!/FINISH=%0,%1,CANCEL,Canceled;@set %q<ticket>=STATUS:Canceled

&CAN_REOPEN #tick=cor(match(get(%0/CLAIMED),%1),u(FIL.BUCKET_CANADMIN,get(%0/BUCKET),%1))
&HEAR_REOPEN #tick=u(CAN_REPLY,%0,%1)

&SW.TICKET.REOPEN #tick=@attach %!/FINISH=%0,%1,REOPEN,Re-opened;@set %q<ticket>=STATUS:Pending

&CAN_DUE #tick=cor(match(get(%0/CLAIMED),%1),u(FIL.BUCKET_CANADMIN,get(%0/BUCKET),%1))
&HEAR_DUE #tick=u(CAN_REPLY,%0,%1)

&SW.TICKET.DUE #tick=@attach %!/FINDTICKET=%0,ticket;@assert/inline u(CAN_DUE,%q<ticket>,%:)=@pemit %#=Permission denied.

&SW.TICKET.SCAN #tick=@assert/inline words(setr(!,filter(FIL.BUCKET_CANADMIN,u(FN.BUCKETS),%B,%B,%:),vbuck))=@pemit %#=No buckets you can scan!;@pemit %#=header(Bucket Scan);@dolist/inline %q<vbuck>={@if words(setr(!,filter(FN.NEWACTIVITY,filterobjid(get(%d0/TICKETS)),%b,%b,%:),unchecked))={@pemit %#=cname(%d0) \([words(%q<unchecked>)] New Activity\): [elist(iter(%q<unchecked>,cname(%i0),%b,|),,|)]}};@pemit %#=footer()

&SW.TICKET.NEXT #tick=@assert/inline words(setr(!,filter(FIL.BUCKET_CANADMIN,u(FN.BUCKETS),%B,%B,%:),vbuck))=@pemit %#=No buckets you can scan!;@dolist/inline %q<vbuck>={@if words(setr(!,filter(FN.NEWACTIVITY,filterobjid(get(%d0/TICKETS)),%b,%b,%:),unchecked))={@attach %!/DISPLAY_TICKET=%i0;@break 1}};@pemit %#=No unchecked tickets to catch up on!

&SW.TICKET.PENDING #tick=@attach %!/LIST_TICKETS=Visible Pending Tickets,filter(FIL.STATUS,u(FN.VISIBLE_TICKETS,%:),%b,%b,Pending)

&HEAR_ARCHIVE #tick=u(HEAR_REPLY,%0,%1)

&SW.TICKET.ARCHIVE #tick=@attach %!/FINDTICKET=%0,ticket;@assert/inline match(Completed Denied Canceled,get(%q<ticket>/STATUS))=@pemit %#=Cannot archive a ticket that hasn't been finished somehow!;@attach %!/ADDEVENT=%q<ticket>,%:,ARCHIVE,Archived the Ticket.;@attach %!/TICKET_ALERT=Archived Ticket.,HEAR_ARCHIVE;@snapshot/unload/overwrite %q<ticket>=ticket_[cname(%q<ticket>)];@set [setr(!,get(%q<ticket>/BUCKET),bucket)]=TICKETS:[listdiff(filterobjid(get(%q<bucket>/TICKETS),%q<ticket>)];@set %q<ticket>=!INDESTRUCTIBLE !SAFE;@destroy/override %q<ticket>

@@ Myjob family
&SWITCHES.MYTICKETS.0 #tick=CREATE|NEW|REPLY|STAFFCOMMENT|COMPLETE|DENY|CANCEL|REOPEN|DUE|OLD
&SWITCHES.MYTICKETS.5 #tick=ARCHIVE

&SW.MYTICKETS.CREATE #tick=@attach %!/SW.TICKET.CREATE
&SW.MYTICKETS.NEW #tick=@attach %!/SW.TICKET.CREATE
&SW.MYTICKETS.REPLY #tick=@attach %!/SW.TICKET.REPLY
&SW.MYTICKETS.STAFFCOMMENT #tick=@attach %!/SW.TICKET.STAFFCOMMENT
&SW.MYTICKETS.COMPLETE #tick=@attach %!/SW.TICKET.COMPLETE
&SW.MYTICKETS.DENY #tick=@attach %!/SW.TICKET.DENY
&SW.MYTICKETS.CANCEL #tick=@attach %!/SW.TICKET.CANCEL
&SW.MYTICKETS.REOPEN #tick=@attach %!/SW.TICKET.REOPEN
&SW.MYTICKETS.DUE #tick=@attach %!/SW.TICKET.DUE
&SW.MYTICKETS.ARCHIVE #tick=@attach %!/SW.TICKET.ARCHIVE

&SW.MYTICKETS.MAIN #tick=@attach %!/LIST_TICKETS=Visible Pending Tickets,filter(FIL.STATUS,u(FN.VISIBLE_TICKETS,%:),%b,%b,Pending)

&FIL.NSTATUS #tick=!u(FIL.STATUS,%0,%1)

&SW.MYTICKETS.OLD #tick=@attach %!/LIST_TICKETS=Visible Pending Tickets,filter(FIL.NSTATUS,u(FN.VISIBLE_TICKETS,%:),%b,%b,Pending)

+help/add IssueTracker/+bucket=#tick/HLP.BUCKET
&HLP.BUCKET #tick=The Issue Ticket Tracker is split between two commands: +ticket and +ticket. It is used to track ongoing requests, tasks, jobs, etc. +ticket is a largely admin-oriented command.%R%R[ansi(hc,Administration Commands)]%R%R[ansi(hw,+ticket/create <name>\[=<description>\])]%R%TCreate a new Bucket. Can optionally provide a short description.%R%R[ansi(hw,+ticket/rename <name>=<new name>)]%R%TRename a bucket.%R%R[ansi(hw,+ticket/delete <bucket>=<name or objid>)]%R%TDelete a bucket. Can only work if emptied of tickets.%R%R[ansi(hw,+ticket/describe <bucket>=<description>)]%R%TSet a description.%R%R[ansi(hw,+ticket/grant <bucket>=<player>)]%R%TAssign a player as a bucket administrator able to claim and operate on tickets.%R%R[ansi(hw,+ticket/revoke <bucket>=<player>)]%R%TRevoke said privileges.%R%R[ansi(hw,+ticket/options <bucket>\[/<option>=<value>\])]%R%TView or set bucket options such as due date.%R%R[ansi(hw,+ticket/lock <bucket>/<locktype>=<lock key>)]%R%TLock a Bucket. Locktype can be POST or ADMIN. PostLock is used for who can create a Ticket in this category. AdminLock is an alternate way of /grant'ing the right to manage the bucket.%R%R[ansi(hw,+ticket/unlock <bucket>/<locktype>)]%R%TRestore the default locks.%R%R[ansi(hc,Player Commands)]%R%R[ansi(hw,+ticket)]%R%TList all Buckets you can create tickets to.%R%R[ansi(hw,+ticket <bucket>)]%R%TView all tickets for that bucket. Requires management rights.

+help/add IssueTracker/+ticket=#tick/HLP.TICKET
&HLP.TICKET #tick=The Issue Ticket Tracker is split between two commands: +ticket and +ticket. It is used to track ongoing requests, tasks, jobs, etc. +ticket is the more commonly used command.%R[ansi(hc,Aliases:)] +ticket, +job, +jobs, +tickets... also +myjob(s) and @myjob(s)%R%R[ansi(hc,Ticket Management)]%R%R[ansi(hw,+ticket)]%R%TList all Tickets you can see. That being: those of a Bucket you have rights to, those you own, are a handler, or a helper of.%R%R[ansi(hw,+ticket <ticket>)]%R%TDisplay details about a Ticket, including its ongoing discussion thread.%R%R[ansi(hw,+ticket/pending)]%R%TShow only Pending Tickets.%R%R[ansi(hw,+ticket/create <bucket>/<title>=<description>)]%R%TSubmits a new Ticket.%R%R[ansi(hw,+ticket/reply <ticket>=<text>)]%R%TAdd a new message to the ongoing discussion thread for the ticket.%R%R[ansi(hw,+ticket/staffcomment <ticket>=<text>)]%R%TAdd a comment that only Handlers/Staff/Bucket-Admin can see as part of the ticket's thread.%R%R[ansi(hw,+ticket/claim <ticket>)]%R%TAssign yourself as a Ticket's handler. Must be a Bucket admin to do this.%R%R[ansi(hw,+ticket/unclaim <ticket>)]%R%TStop being a Ticket's handler.%R%R[ansi(hw,+ticket/assign <ticket>=<player>)]%R%TAssign a player as a Ticket's handler.%R%R[ansi(hw,+ticket/unassign <ticket>=<player>)]%R%TRemove a Ticket's handler.%R%R[ansi(hw,+ticket/addhelper <ticket>=<player>)]%R%TAdd a Helper to a ticket. A Helper can see everything the owner does and use /reply.%R%R[ansi(hw,+ticket/remhelper <ticket>=<player>)]%R%TRemoves a ticket's Helper.%R%R[ansi(hw,+ticket/complete <ticket>=<text>)]%R%TMark a Ticket as completed with a final message. Only Handlers/Admin can do it.%R%R[ansi(hw,+ticket/deny <ticket>=<text>)]%R%TMark a Ticket as denied with a final message. Only Handlers/Admin can do it.%R%R[ansi(hw,+ticket/cancel <ticket>=<text>)]%R%TMark a Ticket as canceled with a final message. Only Handlers/Admin and the Owner can do it.%R%R[ansi(hw,+ticket/reopen <ticket>=<text>)]%R%TMark a finished Ticket as Pending with a new message. Used on those which are Completed, Denied, or Canceled. Only Handlers/Admin can do it.%R%R[ansi(hw,+ticket/due <ticket>=<duration>)]%R%TSet a new Due Date on a Ticket. <duration> is something like 8d 5h for 8 days, 5 hours.%R%R[ansi(hw,+ticket/scan)]%R%TShow an index of all visible Tickets with new activity.%R%R[ansi(hw,+ticket/next)]%R%TShow the next visible Ticket with new activity as if you had used [ansi(hw,+ticket <ticket>)] on it.%R%R[ansi(hw,+ticket/archive <ticket>)]%R%TSave a copy of a finished Ticket to deep/cold storage and delete it from the Ticket Queue. Wizard Only. Difficult to retrieve, so this is best used for very old tickets cluttering the queue.%R%R[ansi(hc,+myjob Commands)]%RThese are provided for those who are very accustomed to Anomaly Jobs.%R%R[ansi(hw,+myjob)] or [ansi(hw,+myjobs)]%R%TDisplay all currently open Tickets you own or have been assigned.%R%R[ansi(hw,+myjob/old)] or [ansi(hw,+myjobs/old)]%R%TShow all closed Tickets instead.%R%R+myjob also supports many of the switches of +ticket, like /reply and /complete.

